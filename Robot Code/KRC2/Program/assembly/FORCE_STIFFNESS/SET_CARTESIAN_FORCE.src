&ACCESS RO
&REL 7
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL SET_CARTESIAN_FORCE(DIRECTION:IN, PROFILE:IN, ACTIVATION:IN, TIME:IN, AMPLITUDE:IN, DURATION:IN, DEFLECT:IN, VELOCITY:IN, RISE:IN, FALL:IN, FREQ:IN, PHASE:IN, OFFSET:IN )

CHAR DIRECTION
INT PROFILE, ACTIVATION, TIME
REAL AMPLITUDE, DURATION
REAL DEFLECT, VELOCITY
REAL RISE, FALL
REAL FREQ, PHASE, OFFSET


DECL INT md_int[16]
DECL REAL md_real[16]
DECL CHAR FORCE_DIR[13]
DECL INT n
DECL INT result

FOR n=1 TO 16
 md_int[n] = 0
 md_real[n] = 0
ENDFOR

SWITCH DIRECTION

CASE "X"
 FORCE_DIR[] = "DESIREDFORCEX"

CASE "Y"
 FORCE_DIR[] = "DESIREDFORCEY"

CASE "Z"
 FORCE_DIR[] = "DESIREDFORCEZ"

DEFAULT

ENDSWITCH

IF PROFILE > 2 THEN
 md_int[1] = 0
ELSE
 md_int[1] = PROFILE
ENDIF

IF ACTIVATION > 1 THEN
 md_int[2] = 0
ELSE
 md_int[2] = ACTIVATION
ENDIF

IF TIME > 1 THEN
 md_int[3] = 0
ELSE
 md_int[3] = TIME
ENDIF

md_real[1] = AMPLITUDE
md_real[2] = DURATION
md_real[3] = DEFLECT
md_real[4] = VELOCITY

IF PROFILE == 0 THEN
 md_real[5] = FREQ
 md_real[6] = PHASE
 md_real[7] = OFFSET
 md_real[8] = RISE
 md_real[9] = FALL
ELSE
 md_real[5] = RISE
 md_real[6] = FALL
ENDIF

result = MD_CMD("PAPAS", FORCE_DIR[], md_int[], md_real[])

RETURN TRUE

ENDFCT