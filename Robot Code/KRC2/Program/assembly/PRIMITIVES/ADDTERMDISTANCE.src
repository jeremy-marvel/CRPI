&ACCESS RVP
&REL 4
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL ADDTERMDISTANCE(RTYPE:IN,X:IN, Y:IN, Z:IN, TOTAL:IN)
  ;FOLD DECLARE FUNCTION ARGUMENTS
  DECL RESULT_TYPE RTYPE
  DECL REAL X
  DECL REAL Y
  DECL REAL Z
  DECL REAL TOTAL
  ;ENDFOLD

  ; ------------------------------------------------------
  ; AUTHOR:    JEREMY MARVEL
  ; SYSTEM:    KUKA ASSEMBLY PRIMITIVES
  ; SUBSYSTEM: HELPER FUNCTIONS
  ; FILE:      ADDTERMDISTANCE.SRC
  ;
  ; Declare and configure a distance-based termination
  ; condition
  ; -----------------------------------------------------

  DECL TERM_COND CONDITS
  BOOL RETURNME

  CONDITS={TTYPE #NONE,RTYPE #SUCCESS,RESULT_ FALSE,THRESHOLD_ 0.0,TIMER_NO_ 1,BEGIN_TIME_ 0.0,END_TIME_ 0.0,CUR_TIME_ 0.0,LAST_VAL_ 0.0,DIGITAL_IN_ 0,ORIGIN_ {X 0.0,Y 0.0,Z 0.0,A 0.0,B 0.0,C 0.0},DELTA_X_ 0.0,DELTA_Y_ 0.0,DELTA_Z_ 0.0}
  RETURNME = FALSE

  CONDITS.TTYPE = #DISTANCE
  IF X <= 0 THEN
    CONDITS.DELTA_X_ = -1
  ELSE
    CONDITS.DELTA_X_ = X
    RETURNME = TRUE
  ENDIF

  IF Y <= 0 THEN
    CONDITS.DELTA_Y_ = -1
  ELSE
    CONDITS.DELTA_Y_ = Y
    RETURNME = TRUE
  ENDIF

  IF Z <= 0 THEN
    CONDITS.DELTA_Z_ = -1
  ELSE
    CONDITS.DELTA_Z_ = Z
    RETURNME = TRUE
  ENDIF

  IF TOTAL <= 0 THEN
    CONDITS.THRESHOLD_ = -1
  ELSE
    CONDITS.THRESHOLD_ = TOTAL
    RETURNME = TRUE
  ENDIF

  IF RETURNME THEN
    CONDITS.RTYPE = RTYPE
    RETURNME = RETURNME AND ADD_TERM(CONDITS)
  ENDIF

  RETURN(RETURNME)
ENDFCT