&ACCESS RVP
&REL 2
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL ADDSEARCHLINEAR(X_OFFSET:IN, Y_OFFSET:IN, Z_OFFSET:IN, SPEED:IN)
  ;FOLD DECLARE FUNCTION ARGUMENTS
  DECL REAL X_OFFSET
  DECL REAL Y_OFFSET
  DECL REAL Z_OFFSET
  DECL REAL SPEED
  ;ENDFOLD

  ; ------------------------------------------------------
  ; AUTHOR:    JEREMY MARVEL
  ; SYSTEM:    KUKA ASSEMBLY PRIMITIVES
  ; SUBSYSTEM: HELPER FUNCTIONS
  ; FILE:      ADDSEARCHLINEAR.SRC
  ;
  ; Declare and configure a linear search motion
  ; -----------------------------------------------------

  DECL SEARCH_PARAM PARAM
  BOOL RETURNME

  PARAM={STYPE #NONE,SPEED 0.0,LENGTH 0.0,WIDTH 0.0,TURNS 0,X 0.0,Y 0.0,Z 0.0,MAGNITUDE 0.0,RADIUS 0.0,RADIUS_OFFSET_ 0.0,TOTAL_LENGTH_ 0.0,LENGTH_STEP_ 0.0,LENGTH_DELTA_ 0.0,X_OFFSET_ 0.0,Y_OFFSET_ 0.0,Z_OFFSET_ 0.0,DEG_OFFSET_ 0.0,DEG_OFFSET_DELTA_ 0.0,THETA_MAX_ 0.0,RASTER_RATIO_ 0.0}
  RETURNME = TRUE

  PARAM.STYPE = #LINEAR
  PARAM.SPEED = ABS(SPEED)
  PARAM.X = X_OFFSET
  PARAM.Y = Y_OFFSET
  PARAM.Z = Z_OFFSET

  RETURNME = MOTION_CFG(PARAM)
  RETURNME = RETURNME AND INIT_SEARCH(PARAM)

  RETURN(RETURNME)
ENDFCT