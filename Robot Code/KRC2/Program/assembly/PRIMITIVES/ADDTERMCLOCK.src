&ACCESS RVO
&REL 8
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL ADDTERMCLOCK(TTYPE:IN, RTYPE:IN, TIMEOUT:IN, TIMER_NO:IN)
  ;FOLD DECLARE FUNCTION ARGUMENTS
  DECL TERM_TYPE TTYPE
  DECL RESULT_TYPE RTYPE
  DECL REAL TIMEOUT
  DECL INT TIMER_NO
  ;ENDFOLD

  ; ------------------------------------------------------
  ; AUTHOR:    JEREMY MARVEL
  ; SYSTEM:    KUKA ASSEMBLY PRIMITIVES
  ; SUBSYSTEM: HELPER FUNCTIONS
  ; FILE:      ADDTERMCLOCK.SRC
  ;
  ; Declare and configure a timer-based termination
  ; condition
  ; -----------------------------------------------------

  DECL TERM_COND CONDITS
  BOOL RETURNME

  CONDITS={TTYPE #NONE,RTYPE #SUCCESS,RESULT_ FALSE,THRESHOLD_ 0.0,TIMER_NO_ 1,BEGIN_TIME_ 0.0,END_TIME_ 0.0,CUR_TIME_ 0.0,LAST_VAL_ 0.0,DIGITAL_IN_ 0,ORIGIN_ {X 0.0,Y 0.0,Z 0.0,A 0.0,B 0.0,C 0.0},DELTA_X_ 0.0,DELTA_Y_ 0.0,DELTA_Z_ 0.0}
  RETURNME = TRUE

  SWITCH TTYPE
  CASE #ALARMCLOCK
    CONDITS.TTYPE = TTYPE
    CONDITS.RTYPE = RTYPE
    CONDITS.BEGIN_TIME_ = 0.0
    CONDITS.END_TIME_ = ABS(TIMEOUT)
    CONDITS.TIMER_NO_ = ABS(TIMER_NO)
  CASE #PROGTIME
    CONDITS.TTYPE = TTYPE
    CONDITS.RTYPE = RTYPE
    CONDITS.BEGIN_TIME_ = 0.0
    CONDITS.END_TIME_ = ABS(TIMEOUT)
    CONDITS.CUR_TIME_ = 0.0
    CONDITS.LAST_VAL_ = 0.0
    CONDITS.TIMER_NO_ = ABS(TIMER_NO)
  DEFAULT
    RETURNME = FALSE
  ENDSWITCH

  IF CONDITS.TIMER_NO_ > 32 THEN
    RETURNME = FALSE
  ENDIF

  IF RETURNME THEN
    RETURNME = RETURNME AND ADD_TERM(CONDITS)
  ENDIF

  RETURN(RETURNME)
ENDFCT