&ACCESS RVP1
&REL 62
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL MOTION_CFG(PARAM:OUT, FIRST_RUN:IN, RESET_VALS:IN)
  ;FOLD DECLARE FUNCTION ARGUMENTS
  DECL SEARCH_PARAM PARAM
  DECL BOOL FIRST_RUN
  DECL BOOL RESET_VALS
  ;ENDFOLD
  ;FOLD DECLARE LOCAL ARGUMENTS
  REAL CUR_FREQ
  INT T_NO
  ; ENDFOLD
  T_NO = 1

  ; Configure the offsets based on the calculated update
  ; frequency.

;  IF FIRST_RUN THEN
;    CUR_FREQ = GET_FREQ(T_NO, TRUE)
;    CUR_FREQ = 1000.0
;  ELSE
;    CUR_FREQ = GET_FREQ(T_NO, FALSE)
;  ENDIF
  CUR_FREQ = LAST_KNOWN_FREQ


  SWITCH PARAM.STYPE
    CASE #SPIRAL
      PARAM.THETA_MAX_ = PARAM.TURNS * 360.0
      PARAM.RADIUS_OFFSET_ = PARAM.RADIUS / PARAM.THETA_MAX_
      PARAM.DEG_OFFSET_DELTA_ = MIN (PARAM.SPEED, 360.0)
      PARAM.DEG_OFFSET_DELTA_ = PARAM.DEG_OFFSET_DELTA_ / CUR_FREQ
    CASE #RASTER
      IF EVEN (PARAM.TURNS) THEN
        PARAM.LENGTH_STEP_ = PARAM.LENGTH / (PARAM.TURNS - 1)
        PARAM.TOTAL_LENGTH_ = (PARAM.TURNS * PARAM.WIDTH) + PARAM.LENGTH
      ELSE
        PARAM.LENGTH_STEP_ = PARAM.LENGTH / PARAM.TURNS
        PARAM.TOTAL_LENGTH_ = ((PARAM.TURNS+1) * PARAM.WIDTH) + PARAM.LENGTH
      ENDIF
      PARAM.LENGTH_DELTA_ = PARAM.SPEED / CUR_FREQ
      PARAM.RASTER_RATIO_ = PARAM.WIDTH / (PARAM.WIDTH + PARAM.LENGTH_STEP_)
    CASE #CIRCLE
      PARAM.DEG_OFFSET_DELTA_ = MIN(PARAM.SPEED, 360.0) / CUR_FREQ
    CASE #ROTATE
      PARAM.DEG_OFFSET_DELTA_ = MIN(PARAM.SPEED, 360.0) / CUR_FREQ
    CASE #HOP
      PARAM.DEG_OFFSET_DELTA_ = PARAM.SPEED * CUR_FREQ
    CASE #LINEAR
      PARAM.TOTAL_LENGTH_ = SQRT((PARAM.X * PARAM.X) + (PARAM.Y * PARAM.Y) + (PARAM.Z * PARAM.Z))
      PARAM.LENGTH_STEP_ = PARAM.SPEED / CUR_FREQ
      PARAM.LENGTH_DELTA_ = PARAM.TOTAL_LENGTH_ / PARAM.SPEED
      PARAM.X_OFFSET_ = (PARAM.X / PARAM.LENGTH_DELTA_) / CUR_FREQ
      PARAM.Y_OFFSET_ = (PARAM.Y / PARAM.LENGTH_DELTA_) / CUR_FREQ
      PARAM.Z_OFFSET_ = (PARAM.Z / PARAM.LENGTH_DELTA_) / CUR_FREQ
    CASE #CONSTOFFSET
      PARAM.SPEED = 30.0
      PARAM.TOTAL_LENGTH_ = SQRT((PARAM.X * PARAM.X) + (PARAM.Y * PARAM.Y) + (PARAM.Z * PARAM.Z))
      PARAM.LENGTH_STEP_ = PARAM.SPEED / CUR_FREQ
      PARAM.LENGTH_DELTA_ = PARAM.TOTAL_LENGTH_ / PARAM.SPEED
      PARAM.X_OFFSET_ = (PARAM.X / PARAM.LENGTH_DELTA_) / CUR_FREQ
      PARAM.Y_OFFSET_ = (PARAM.Y / PARAM.LENGTH_DELTA_) / CUR_FREQ
      PARAM.Z_OFFSET_ = (PARAM.Z / PARAM.LENGTH_DELTA_) / CUR_FREQ
  ENDSWITCH

  RETURN (TRUE)
ENDFCT