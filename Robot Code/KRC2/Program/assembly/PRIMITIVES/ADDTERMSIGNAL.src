&ACCESS RVP
&REL 4
&PARAM TEMPLATE = C:\KRC\Roboter\Template\FunctionVorgabe
&PARAM EDITMASK = *
DEFFCT BOOL ADDTERMSIGNAL(TTYPE:IN, RTYPE:IN, SIGL:IN)
  ;FOLD DECLARE FUNCTION ARGUMENTS
  DECL TERM_TYPE TTYPE
  DECL RESULT_TYPE RTYPE
  DECL REAL SIGL
  ;ENDFOLD

  ; ------------------------------------------------------
  ; AUTHOR:    JEREMY MARVEL
  ; SYSTEM:    KUKA ASSEMBLY PRIMITIVES
  ; SUBSYSTEM: HELPER FUNCTIONS
  ; FILE:      ADDTERMSIGNAL.SRC
  ;
  ; Declare and configure a signal-based termination
  ; condition
  ; -----------------------------------------------------

  DECL TERM_COND CONDITS
  BOOL RETURNME

  CONDITS={TTYPE #NONE,RTYPE #SUCCESS,RESULT_ FALSE,THRESHOLD_ 0.0,TIMER_NO_ 1,BEGIN_TIME_ 0.0,END_TIME_ 0.0,CUR_TIME_ 0.0,LAST_VAL_ 0.0,DIGITAL_IN_ 0,ORIGIN_ {X 0.0,Y 0.0,Z 0.0,A 0.0,B 0.0,C 0.0},DELTA_X_ 0.0,DELTA_Y_ 0.0,DELTA_Z_ 0.0}
  RETURNME = TRUE

  SWITCH TTYPE
  CASE #EXTSIGNAL
    CONDITS.TTYPE = TTYPE
    CONDITS.RTYPE = RTYPE
    CONDITS.DIGITAL_IN_ = ABS(SIGL)
  CASE #CONTACT
    CONDITS.TTYPE = TTYPE
    CONDITS.RTYPE = RTYPE
    CONDITS.THRESHOLD_ = ABS(SIGL)
  DEFAULT
    RETURNME = FALSE
  ENDSWITCH

  IF RETURNME THEN
    RETURNME = RETURNME AND ADD_TERM(CONDITS)
  ENDIF

  RETURN(RETURNME)
ENDFCT